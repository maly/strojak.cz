---
id: 212
title: '6502 &#8211; !etěvs ,johA'
date: 2014-02-28T15:30:32+00:00
author: Martin Maly
layout: post
guid: http://strojak.uelectronics.info/?p=212
permalink: /6502-etevs-joha/
dsq_thread_id:
  - "2335592322"
categories:
  - "6502"
---
Všechno jde udělat i jinak, hlavně v osmibitovém assembleru. Takže ani pro Hello, world není jen jediný předepsaný postup.

<!--more-->

V [první ukázce](http://strojak.uelectronics.info/6502-ahoj-svete/ "6502 – Ahoj, světe…") jsem použil postup, kterému se říká ASCIIZ &#8211; tedy ASCII řetězec, ukončený bajtem  s hodnotou 0 (ASCII + Zero). Tento způsob zápisu řetězců používají třeba překladače jazyka C &#8211; kdo z vás zná Céčko, tak ví.

Jazyk Pascal používal jiný přístup &#8211; první bajt udával délku řetězce ve znacích, a pak následovaly kýžené znaky. Někdy může být tento přístup výhodnější &#8211; hlavně tehdy, když nám délku spočítá překladač a natvrdo uloží do kódu. Jak by se to přepsalo do assembleru 6502?

Tak, základ zůstane stejný &#8211; definice konstant, inicializace i rutina SEROUT, jen ten vnitřek se změní. Registr X použijeme jako počítadlo a index znaku v řetězci. Když dosáhne hodnoty rovné délce řetězce, přestaneme. Ta zásadní pasáž kódu bude vypadat nějak takhle:

<pre class="lang:asm decode:true">; Začínáme vypisovat znaky 
TOUT:     LDX     #0 ; pozice
TLOOP:    LDA     text,x 
          JSR     SEROUT
          INX     ; X++
          CPX	  #tsize ; už jsme na konci?
          BNE     tloop ; pokud ne (tj. X&lt;délka), tak pokračujeme

ENDLOOP:          
          JMP     endloop 

TEXT:     DB      $0C,"My hovercraft is full of eels!",$0D,$0A
TSIZE     EQU     $-TEXT</pre>

## A co Woz?

Logout mě [upozornil](http://blog.i-logout.cz/vsechno-je-jednou-poprve.php), že Steve Wozniak v monitoru pro Apple I použil trik, kterým ušetřil několik bajtů (a tedy taktů procesoru). Ačkoli mi jeho trik připadá v tomto případě jako klasický příklad &#8222;overengineeringu&#8220; (tedy optimalizace až přehnaná), tak si ji ukážeme, protože ilustruje schopnost podívat se na problém z naprosto neobvyklého úhlu. Což se zase při programování v assembleru často hodí.

Úvodní úvaha je jednoduchá: na začátku se nuluje registr X, v průběhu se pak zvyšuje o 1 a kontroluje se, jestli už má hodnotu N. Kdybychom na začátku do registru X uložili hodnotu N a šli v opačném pořadí, tedy směrem k nule, tak by odpadla instrukce porovnání a konec by nastal ve chvíli, kdy instrukce DEX (X=X-1) dojde k nule. Pak nastaví příznak Z (viz [popis instrukce](http://strojak.uelectronics.info/instrukce-6502-aritmetika/ "Instrukce 6502 – aritmetika")).

Nese to s sebou jeden drobný problém: čtení znaků by fungovalo od konce. Hm, a co? Tak je tam zapíšeme pozpátku!

<pre class="lang:asm decode:true">; Začínáme vypisovat znaky 
TOUT:     LDX     #tsize ; X je počet znaků, co zbývý vypsat
TLOOP:    LDA     text-1,x 
                  ; A protože X jde od hodnoty TSIZE k nule,
                  ; tak se znaky berou od konce
          JSR     SEROUT
          DEX     ; X-- 
          BNE     tloop ; a dokud není 0, tak pokračujeme

ENDLOOP:          
          JMP     endloop 

TEXT:     DB      $0a,$0d,"slee fo lluf si tfarcrevoh yM",$0c
TSIZE     EQU     $-TEXT</pre>

Ušetřili jsme dva bajty a nějaký ten takt, přišli jsme o snadnou čitelnost. Ano, při programování osmibitů jsou situace, kdy dva bajty či pár taktů znamená hodně. (Spousta programátorů, co na assemblerech vyrostla, pak logicky považuje cokoli jiného za plýtvání ad absurdum.)

## Ještě nějaký trik, prosím!

_&#8222;Ale no jistě,_&#8220; odpovědělo sluchátko. Pojďte se podívat na následující kód:



Přeložte si ho, spusťte emulátor a pojďme krokovat:

<pre class="lang:asm decode:true">0000                  .ORG 0 
0000 A2 FF            LDX #$FF 
0002 9A               TXS 
0003 20 09 00         JSR label 
0006 4C 00 00         JMP 0 
0009 60        LABEL:   RTS</pre>

První instrukce (adresa 0000, 2 bajty) nastaví X na hodnotu FFh, druhá (adresa 0002, 1 bajt) tuto hodnotu zkopíruje do ukazatele zásobníku S. Třetí instrukce (adresa 0003, 3 bajty) je [instrukce volání podprogramu](http://strojak.uelectronics.info/instrukce-6502-skoky-a-podprogramy/ "Instrukce 6502 – skoky a podprogramy"). Volá se podprogram na adrese 0009&#8230;

V tuto chvíli se zastavíme a podíváme se na vrchol zásobníku, co se tam uložilo. Víme, že instrukce JSR ukládá dva bajty návratové adresy. Protože byl ukazatel nastaven na FFh, budou tyto dva bajty na adresách 01FEh a 01FFh. Co tam najdeme?

Na zásobníku je uložena hodnota 0005. Vidíme, že to není adresa instrukce za voláním JSR (ta je 0006), ale o 1 nižší. Instrukce RTS vezme hodnotu ze zásobníku, k ní přičte 1 a na tu adresu skočí.

K čemu nám bylo tohle mentální cvičení? Ukážeme si totiž jeden princip, který se u osmibitových assemblerů používá docela často, a nejčastěji právě u výpisu různých textů. Pokud se text vyskytuje v programu jen jednou a je konstantní (tj. typicky nějaké hlášení), tak je pro programátora pohodlné napsat ho přímo do kódu, tam, kde potřebuje. Ušetří tím sekvenci &#8222;zadej někam adresu hlášení, co chceš vypsat &#8211; zavolej rutinu, která vypíše řetězec ze zadané adresy&#8220;. Místo toho jen zavolá podprogram, jehož funkce se dá popsat slovy &#8222;vypiš znaky, co se nacházejí za instrukcí JSR, a až narazíš na ukončovací znak 00, tak se vrať za tu nulu, tam pokračuje program.&#8220;

Nějak takhle (stále upravujeme kód z předchozího příkladu):

<pre class="lang:asm decode:true">JSR     PRIMM 
          DB      $0C,"My hovercraft is full of eels!",$0D,$0A,$00 

DONE:     JMP     DONE ; TO JE KONEC!!! 🙁</pre>

Vidíte, že je tam instrukce volání podprogramu PRIMM (PRint IMMediately), za ní jsou přímo znaky požadované hlášky, ukončené nulou, a za tím zase pokračuje program.

Co musí udělat podprogram PRIMM? Představte si, že je vyvolán. V tu chvíli je na zásobníku &#8222;adresa instrukce za JSR &#8211; 1&#8220;. Ukazatel SP je jeden bajt POD touto hodnotou, návratová adresa je tedy na adresách SP+1 a SP+2.

Nejdřív si uložíme pracovní registry A, X a Y &#8211; tím se SP sníží o 3 a situace na zásobníku bude vypadat takto:

<table>
  <tr>
    <td>
      SP+5
    </td>
    
    <td>
      Vyšší bajt návratové adresy
    </td>
  </tr>
  
  <tr>
    <td>
      SP+4
    </td>
    
    <td>
      Nižší bajt návratové adresy
    </td>
  </tr>
  
  <tr>
    <td>
      SP+3
    </td>
    
    <td>
      Obsah registru A
    </td>
  </tr>
  
  <tr>
    <td>
      SP+2
    </td>
    
    <td>
      Obsah registru X
    </td>
  </tr>
  
  <tr>
    <td>
      SP+1
    </td>
    
    <td>
      Obsah registru Y
    </td>
  </tr>
  
  <tr>
    <td>
      SP
    </td>
    
    <td>
      První volná pozice na zásobníku
    </td>
  </tr>
</table>

Takže na adrese SP + 0100h + 4 je nižší bajt návratové adresy, na adrese SP + 0100h + 5 je vyšší. Tuto hodnotu si můžeme někam zkopírovat &#8211; ideálně do zero page do dvou buněk vedle sebe. Výhodně pak využijeme [adresní mód IZY](http://strojak.uelectronics.info/adresni-mody-6502/ "Adresní módy 6502"). Připomeňme si: tento mód vezme adresu ze dvou vedle sebe ležících paměťových míst, k té adrese přičte obsah registru Y a výsledek udává adresu, kam se má sahat pro data.

Jakmile narazíme na konec řetězce (anebo nám přeteče registr Y), tak končíme s vypisováním. Teď je potřeba vzít tu původní adresu, k ní přičíst počet vypsaných znaků (tedy registr Y), tu pak zase zapsat na zásobník &#8211; a pak už jen standardně vrátit obsah registrů a provést RTS.

A protože vlastní studium zdrojového kódu řekne víc, než sáhodlouhé popisy, tak bez dalšího vysvětlování &#8211; podprogram PRIMM:

<pre class="lang:asm decode:true">PRIMM:            
          PHA     ; Uložím A
          TXA
          PHA     ; Uložím X
          TYA
          PHA     ; Uložím Y
          TSX     ; Ukazatel na zásobník si načtu do X
          LDA     $0104,X ; Nižší byte návratové adresy 
          		  ; ($0100 je základní adresa zásobníku, X je tu aktuální
                  ; ukazatel zásobníku, +4 proto, že ukazatel SP ukazuje na
                  ; první volné místo, SP+1 je uložený registr X,
                  ; SP+2 je uložený registr Y, SP+3 je uložený registr A
                  ; (na začátku podprogramu jsme si je ukládali)
                  ; SP+4 a SP+5 jsou nižší a vyšší bajt návratové adresy,
                  ; tedy poslední bajt instrukce JSR

          STA     $00 ; Uložíme do ZP (třeba na adresu 00)
          LDA     $0105,X ; Analogicky vyšší byte návratové adresy...
          STA     $01 ; ... ukládáme do ZP na adresu 01
          LDY     #$01 ; Nastavíme Y na počáteční hodnotu. Měla by to být
          		  ; nula, ale protože víme, že návratová adresa je ve 
                  ; skutečnosti o 1 nižší, než adresa prvního bajtu za JSR,
                  ; tak začneme od jedničky.
PRIM2:            
          LDA     ($00),Y ; Načteme bajt. Adresa je "obsah buněk 00 a 01" + Y
          BEQ     PRIM3 ; Načetli jsme nulu? Tak končíme!

          JSR     SEROUT ; Nenulový znak ale vypíšeme
          INY     ; posuneme se na další adresu
          BNE     PRIM2 ; a pokud jsme ještě nepřetočili počítadlo, tak 
                  ; pokračujeme v tisknutí znaků.
                  ; Když už je Y nulové, tak je načase skončit.

PRIM3:            
          TYA     ; V Y je "počet znaků + 1" - přesuneme do A
          CLC     ; budeme sčítat, je potřeba vynulovat C
          ADC     $00 ; K A si přičteme nižší bajt původní návratové adresy
          STA     $0104,X ; a "podvrhneme" ji do zásobníku
          LDA     #$00 ; Vynulujeme A
          ADC     $01 ; a přičteme hodnotu vyššího byte návratové adresy.
          	      ; Pokud při předchozím sčítání došlo k přenosu, tak se 
                  ; vyšší bajt zvedne o 1, jinak zůstane stejný
          STA     $0105,X ; A opět vyšší bajt návratové hodnoty analogicky 
                  ; uložíme na zásobník a budeme se tvářit, že to tak
                  ; už bylo
          PLA     ; Přečteme uloženou hodnotu
          TAY     ; co patří do registru Y
          PLA     ; a úplně stejně tu, co
          TAX     ; patří do registru X
          PLA     ; ještě původní hodnotu A
          RTS     ; a návrat!</pre>

Můžete si zkusit složit celý zdrojový kód a vyzkoušet, jak hezky funguje. (Nebo si můžete kliknout [sem](http://www.asm80.com/index.html#demo/strojak2.a65) a on se vám automaticky přidá do pracovního prostoru v ASM80 IDE).

(Rutina PRIMM pochází z operačního systému Commodore C64 a je mírně upravena, viz [zdroj](http://www.6502.org/source/io/primm.htm).)

_Pro pozorné: v kódu <del>je jedna chyba, která</del> jsou dvě chyby, které se projeví při určité konstelaci &#8211; zkuste na ně přijít._

Mimochodem &#8211; existuje ještě jeden používaný způsob označování konce řetězců, hlavně u anglických textů. Kromě zadaného počtu znaků + řetězce nebo řetězce ukončeného bajtem 00h (u CP/M služba pro vypisování řetězců používá ukončování znakem $) můžeme použít i trik, který počítá s tím, že anglická abeceda si v ASCII vystačí se znaky z rozsahu 00h-7Fh. Pak stačí poslednímu znaku nastavit nejvyšší bit na 1 (tj. posunout jej do rozsahu 80h-FFh). Ze znaku &#8222;!&#8220; (kód 21h) se tak stane znak s kódem A1h. No a postup je prostý &#8211; před vypsáním znaku použijeme AND s hodnotou 7Fh (abychom nastavili nejvyšší bit na 0), znak vypíšeme a pak zkontrolujeme, jestli není nejvyšší bit roven 1 (u 6502 třeba tak, že ho načteme do registru A &#8211; tím se nejvyšší bit zkopíruje do příznaku N). Pokud ano, byl to poslední znak a my se můžeme vrátit. Napsání rutiny, která bude takto pracovat, nechám už na vás, máte to za domácí úkol&#8230;